plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.jmix' version '2.1.0' apply false
}

group = 'io.github.it-syn'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'io.jmix'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://global.repo.jmix.io/repository/public' }
    }

    jmix {
        bomVersion = '2.1.0'
        projectId = 'menu'
    }

    group = rootProject.group
    version = rootProject.version
    sourceCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options.addStringOption("sourcepath", "")
        options.addStringOption("Xdoclint:none", "-quiet")
    }

    signing {
        sign publishing.publications
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = "io.github.it-syn:${project.archivesBaseName}"
                    description = 'The add-on provides support of multiple main menus in Jmix applications.'
                    url = 'https://github.com/daring2/jmix-menu-editor'
                    packaging = 'jar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    scm {
                        connection = 'sscm:git:git://github.com/daring2/jmix-menu-editor.git'
                        developerConnection = 'scm:git:ssh://github.com:daring2/jmix-menu-editor.git'
                        url = 'https://github.com/daring2/jmix-menu-editor'
                    }
                    developers {
                        developer {
                            id = 'daring'
                            name = 'Danila Valikov'
                            email = 'danila.daring@gmail.com'
                        }
                    }
                }
            }
        }
        repositories.maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }

}
