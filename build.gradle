plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.jmix' version '1.4.2' apply false
}

group = 'ru.itsyn.jmix'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven { url 'https://global.repo.jmix.io/repository/public' }
    }

    jmix {
        bomVersion = '1.4.2'
        projectId = 'menu'
    }

    group = rootProject.group
    version = rootProject.version
    sourceCompatibility = JavaVersion.VERSION_11

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options.addStringOption("sourcepath", "")
    }

    signing {
        sign publishing.publications
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories.maven {
            url = 'https://gitlab.vk.opus.tech/api/v4/projects/6/packages/maven'
            credentials(HttpHeaderCredentials) {
                name = 'Private-Token'
                value = findProperty('vc.gitlabToken')
            }
            authentication { header(HttpHeaderAuthentication) }
        }
    }

}
